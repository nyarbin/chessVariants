%============================================================
%		   METAGAME Game-Playing Workbench
%		  Copyright (c) 1992 Barney D. Pell
%============================================================

% (AMERICAN) CHECKERS as a symmetric chess-like game.
%
% This game implements the full rules of american checkers.
% Each player must make a capture move if one is available,
% and a piece continues capturing until it has no more captures.

game lose_checkers
goals 	stalemate player
	eradicate [ { player } any_piece ]
board_size 8 by 8
board_type planar
promote_rank 8
setup 	man AT {(1,1) (3,1) (5,1) (7,1)
	          (2,2) (4,2) (6,2) (8,2)
	        (1,3) (3,3) (5,3) (7,3)}

constraints must_capture

define 	man
   moving 
	movement
	leap <1,1>
	symmetry {side} 
        end movement
   end moving		

   capturing
	capture by {hop}
	type [{opponent} any_piece]
	effect remove
	movement
		hop before [X=0]
		    over   [X=1]
		    after  [X=0]
		hop_over   [{opponent} any_piece]
	<1,1> symmetry {side}
	end movement
	end capture
   end capturing

   promoting
	promote_to king
   end promoting

   constraints 
	continue_captures	
end define

define 	king
   moving 
	movement
	leap <1,1>
	symmetry {forward side} 
        end movement
   end moving		

   capturing
	capture by {hop}
	type [{opponent} any_piece]
	effect remove
	movement
		hop before [X=0]
		    over   [X=1]
		    after  [X=0]
		hop_over   [{opponent} any_piece]
	<1,1> symmetry {forward side}
	end movement
	end capture
   end capturing

   promoting
	promote_to king
   end promoting

   constraints 
	continue_captures	
end define

end game .

